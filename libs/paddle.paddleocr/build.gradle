plugins {
    id 'com.android.library'
}
import java.security.MessageDigest

android {
    ndkVersion '21.1.6352462'
    compileSdkVersion 32
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 32

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions -Wno-format"
                arguments '-DANDROID_PLATFORM=android-24', '-DANDROID_STL=c++_shared' ,"-DANDROID_ARM_NEON=TRUE"
            }
        }
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
            ldLibs "jnigraphics"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':libs:org.opencv-4.5.5')
}

/**
 * Paddle-Lite包 开发时使用 比如后续更新版本 用于下载新的包
 */
def archives = [
    [
        'src' : 'https://github.com/PaddlePaddle/Paddle-Lite/releases/download/v2.11/inference_lite_lib.android.armv8.clang.c++_shared.with_extra.with_cv.tar.gz',
        'dest': 'PaddleLiteArmv8'
    ],
    [
        'src' : 'https://github.com/PaddlePaddle/Paddle-Lite/releases/download/v2.11/inference_lite_lib.android.armv7.clang.c++_shared.with_extra.with_cv.tar.gz',
        'dest': 'PaddleLiteArmv7'
    ],
]
/**
 * 下载opencv4.5.5源码包 默认为4.2.0 和 AutoJS中的版本不匹配会产生冲突 这里进行修改
 */
def zipArchives = [
    [
        'src' : 'https://github.com/opencv/opencv/releases/download/4.5.5/opencv-4.5.5-android-sdk.zip',
        'dest': 'OpenCV'
    ]
]

/**
 * 首次导入最好手动运行一下这个task下载依赖的包
 */
task downloadAndExtractArchives(type: DefaultTask) {
    doFirst {
        println "Downloading and extracting archives including libs and models"
    }
    doLast {
        // Prepare cache folder for archives
        String cachePath = "cache"
        if (!file("${cachePath}").exists()) {
            mkdir "${cachePath}"
        }
        archives.eachWithIndex { archive, index ->
            MessageDigest messageDigest = MessageDigest.getInstance('MD5')
            messageDigest.update(archive.src.bytes)
            String cacheName = new BigInteger(1, messageDigest.digest()).toString(32)
            // Download the target archive if not exists
            boolean copyFiles = !file("${archive.dest}").exists()
            if (!file("${cachePath}/${cacheName}.tar.gz").exists()) {
                ant.get(src: archive.src, dest: file("${cachePath}/${cacheName}.tar.gz"))
                copyFiles = true; // force to copy files from the latest archive files
            }
            // Extract the target archive if its dest path does not exists
            if (copyFiles) {
                copy {
                    from tarTree("${cachePath}/${cacheName}.tar.gz")
                    into "${archive.dest}"
                }
            }
            // paddle-lite 特殊处理
            // 将v2.11版本paddle-lite的armv8和armv7两个lib包合并到一起
            if (archive.dest.startsWith("PaddleLite")) {
                println "Downloading Paddle-Lite libs"
                String abi = "armeabi-v7a"
                String miniAbi = "armv7"
                println "copy Paddle-Lite: ${archive.dest} ${archive.dest.substring(10)}"
                if (archive.dest.substring(10) == "Armv8") {
                    abi = "arm64-v8a"
                    miniAbi = "armv8"
                    copy {
                        println "copy Paddle-Lite: ${miniAbi} cxx/include into ${abi}"
                        from "${archive.dest}/inference_lite_lib.android.${miniAbi}.clang.c++_shared.with_extra.with_cv/cxx/include"
                        into "PaddleLite/cxx/include"
                    }
                }
                copy {
                    println "copy Paddle-Lite: ${miniAbi} cxx/lib into ${abi}"
                    from "${archive.dest}/inference_lite_lib.android.${miniAbi}.clang.c++_shared.with_extra.with_cv/cxx/lib"
                    into "PaddleLite/cxx/libs/${abi}"
                }
                copy {
                    println "copy Paddle-Lite: ${miniAbi} java/so into ${abi}"
                    from "${archive.dest}/inference_lite_lib.android.${miniAbi}.clang.c++_shared.with_extra.with_cv/java/so"
                    into "PaddleLite/java/libs/${abi}/"
                }
            }
        }
        zipArchives.eachWithIndex { archive, index ->
            MessageDigest messageDigest = MessageDigest.getInstance('MD5')
            messageDigest.update(archive.src.bytes)
            String cacheName = new BigInteger(1, messageDigest.digest()).toString(32)
            // Download the target archive if not exists
            boolean copyFiles = !file("${archive.dest}").exists()
            if (!file("${cachePath}/${cacheName}.zip").exists()) {
                ant.get(src: archive.src, dest: file("${cachePath}/${cacheName}.zip"))
                copyFiles = true; // force to copy files from the latest archive files
            }
            // Extract the target archive if its dest path does not exists
            if (copyFiles) {
                copy {
                    from zipTree("${cachePath}/${cacheName}.zip")
                    into "${archive.dest}"
                }
            }
        }
    }
}
preBuild.dependsOn downloadAndExtractArchives